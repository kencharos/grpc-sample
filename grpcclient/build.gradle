buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
    }
}

plugins {
    id 'java'
}


apply plugin: 'com.google.protobuf'

group 'my'
version '1.0-SNAPSHOT'

sourceCompatibility = 11


def reactiveGrpcVersion = '0.9.0'
def grpcVersion = '1.18.0'
def protobufVersion = '3.6.1'

repositories {
    mavenCentral()
}

dependencies {
    compile "com.google.protobuf:protobuf-java:${protobufVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "com.salesforce.servicelibs:reactor-grpc-stub:${reactiveGrpcVersion}"
    // https://mvnrepository.com/artifact/javax.annotation/javax.annotation-api
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    // https://mvnrepository.com/artifact/io.projectreactor/reactor-core
    compile group: 'io.projectreactor', name: 'reactor-core', version: '3.2.6.RELEASE'
    // https://mvnrepository.com/artifact/io.grpc/grpc-netty
    compile group: 'io.grpc', name: 'grpc-netty', version: grpcVersion

}


protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:${reactiveGrpcVersion}:jdk8@jar"
        }
    }
    generateProtoTasks {

        all().each { task ->
            task.builtins {
                java {
                    outputSubDir = 'generated-proto'
                }
            }
            task.plugins {
                grpc {
                    outputSubDir = 'generated-proto'
                }
            }
            task.plugins {
                reactor {
                    outputSubDir = 'generated-proto'
                }
            }
        }
    }
    generatedFilesBaseDir = "${projectDir}/src/"
}
sourceSets {
    main {
        java {
            srcDir 'src/main/generated-proto'
        }
    }
}